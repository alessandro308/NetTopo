        dnl Process this file with autoconf to produce a configure script.

AC_INIT(ally.c)
dnl aggressively require new autoconf.  "fixes" debian backward compat hack?
AC_PREREQ(2.50) 
dnl Every other copy of the package version number gets its value from here
AM_INIT_AUTOMAKE(ally, 0.1.0)
dnl create a config.h file (Automake will add -DHAVE_CONFIG_H)
AM_CONFIG_HEADER(config.h)
AC_SUBST(VERSION)

ISODATE=`date +%Y-%m-%d`
AC_SUBST(ISODATE)

AC_CANONICAL_HOST
dnl sanity check cached info (nspring) 
AC_NSPRING_CHECK_CACHE

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h stdint.h)

AC_CHECK_HEADER(postgresql/libpq-fe.h,
[AC_DEFINE(HAVE_LIBPQ)
DBPROGS="molly show_aliases recheck_aliases dumpcch"])

dnl Checks for typedefs, structures, and compiler characteristics.

AC_NSPRING_ADT

AC_ARG_ENABLE(prof, [prof: compile for profiling with gprof],
  CFLAGS="$CFLAGS -pg")
AC_ARG_ENABLE(cov, [cov: compile for coverage with gcov],
  CFLAGS="$CFLAGS -fprofile-arcs -ftest-coverage")

dnl emulating AM_WITH_DMALLOC less foolishly.
AC_ARG_WITH(dmalloc, [dmalloc: compile for memory debug],
 AC_CHECK_LIB(dmallocth, malloc)
 AC_CHECK_HEADERS(dmalloc.h)
 AC_MSG_CHECKING(whether dmalloc is complete)
 if test "$ac_cv_lib_dmallocth_malloc" = "yes"; then
   if test "$ac_cv_header_dmalloc_h" = "yes"; then
     AC_MSG_RESULT(yes!)
     AC_DEFINE(WITH_DMALLOC)
   else
     AC_MSG_RESULT(no header)
   fi
 else
   AC_MSG_RESULT(no library)
 fi
)

dnl nspring: to appease src/system.h 
dnl ----- begin appease system.h ------

AC_CHECK_HEADERS( sys/param.h sys/time.h time.h sys/mkdev.h sys/sysmacros.h string.h memory.h fcntl.h dirent.h sys/ndir.h ndir.h alloca.h locale.h )

dnl AC_EGREP_HEADER(utimbuf, utime.h, AC_DEFINE(HAVE_STRUCT_UTIMBUF))
dnl would miss ifdef'd out utimbuf.
AC_CHECK_HEADERS(utime.h error.h)
AC_MSG_CHECKING(for utime)
AC_TRY_COMPILE([#include <sys/types.h>
#include <utime.h>],
[struct utimbuf x; x.actime = x.modtime = 0; utime ("/", &x);],
  [AC_MSG_RESULT(yes)
   AC_DEFINE(HAVE_STRUCT_UTIMBUF)],
  [AC_MSG_RESULT(no)
   AC_CHECK_FUNCS(utimes)])

AC_HEADER_MAJOR
AC_FUNC_ALLOCA
AC_STRUCT_TM
AC_STRUCT_ST_BLOCKS
AC_FUNC_CLOSEDIR_VOID
AC_CHECK_FUNCS(mkfifo)
AC_CHECK_FUNC(mknod)
AC_CHECK_FUNC(vprintf)
AC_CHECK_FUNC(doprnt)

dnl ----- end   appease system.h ------

AC_SUBST(DBPROGS)
AC_OUTPUT(Makefile adt/Makefile )
dnl test -d fish || ( mkdir -p fish && cp $srcdir/fish/Makefile fish )
